; compile all necessary modules
@setup_nadc.idl

; open file
data_dir = '/SCIA/LV2_01/P/'
flname = 'SCI_NL__2PPDPA20040110_085911_000052142023_00165_09738_7265.N1'
SCIA_HL_OPEN, data_dir + flname, dsd, status=status
IF status NE 0 THEN NADC_ERR_TRACE

; read Summary of Quality Flags per State
SCIA_LV2_RD_SQADS, dsd, sqads, status=status
IF status NE 0 THEN NADC_ERR_TRACE

; read Geolocation of the States
SCIA_RD_LADS, dsd, lads, status=status
IF status NE 0 THEN NADC_ERR_TRACE

; read States of the Product
SCIA_LV2_RD_STATE, dsd, state, status=status
IF status NE 0 THEN NADC_ERR_TRACE

; read Geolocation of State
SCIA_LV2_RD_GEO, dsd, geo, status=status
IF status NE 0 THEN NADC_ERR_TRACE

; read Cloud and Aerosol data set
SCIA_LV2_RD_CLD, dsd, cld, status=status
IF status NE 0 THEN NADC_ERR_TRACE
; --- use the CLD data and then release memory...
SCIA_LV2_FREE_CLD, cld

; read DOAS fitting window application data set
indx = WHERE( STRING(dsd[*].name[0:3]) EQ 'DOAS', count )
FOR ii = 0, count-1 DO BEGIN &$
    dsd_name = STRCOMPRESS(STRING(dsd[indx[ii]].name), /REMOVE) &$
    SCIA_LV2_RD_DOAS, dsd_name, dsd, doas, status=status &$
    IF status NE 0 THEN NADC_ERR_TRACE &$
    PRINT, dsd_name, N_ELEMENTS( doas ) &$
; --- use the DOAS results data and then release memory...
    IF SIZE(doas, /TNAME) EQ 'STRUCT' THEN SCIA_LV2_FREE_DOAS, doas &$
ENDFOR

; read BIAS fitting window application data set
indx = WHERE( STRING(dsd[*].name[0:3]) EQ 'BIAS', count )
FOR ii = 0, count-1 DO BEGIN &$
    dsd_name = STRCOMPRESS(STRING(dsd[indx[ii]].name), /REMOVE) &$
    SCIA_LV2_RD_BIAS, dsd_name, dsd, bias, status=status &$
    IF status NE 0 THEN NADC_ERR_TRACE &$
; --- use the BIAS results data and then release memory...
    IF SIZE(bias, /TNAME) EQ 'STRUCT' THEN SCIA_LV2_FREE_BIAS, bias &$
ENDFOR

; close file
stat = SCIA_FCLOSE()
